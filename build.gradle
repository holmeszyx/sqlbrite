buildscript {
  ext.versions = [
      'minSdk': 21,
      'compileSdk': 29,
      'kotlin': '1.3.72',
      'lint': '27.0.1'
  ]

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:4.0.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME
}

ext {
  // Android dependencies.
  supportV4 = 'androidx.legacy:legacy-support-v4:1.0.0'
  supportAnnotations = 'androidx.annotation:annotation:1.0.0'
  supportTestRunner = 'androidx.test.ext:junit:1.1.1'

  supportSqlite = 'androidx.sqlite:sqlite:2.0.0'
  supportSqliteFramework = 'androidx.sqlite:sqlite-framework:2.0.0'

  // Third-party dependencies.
  kotlinStdLib = "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
  dagger = 'com.google.dagger:dagger:2.13'
  daggerCompiler = 'com.google.dagger:dagger-compiler:2.13'
  butterKnifeRuntime = 'com.jakewharton:butterknife:8.8.1'
  butterKnifeCompiler = 'com.jakewharton:butterknife-compiler:8.8.1'
  timber = 'com.jakewharton.timber:timber:4.6.0'
  autoValue = 'com.google.auto.value:auto-value:1.5'
  autoValueParcel = 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
  rxJava = 'io.reactivex.rxjava3:rxjava:3.0.3'
  rxAndroid = 'io.reactivex.rxjava3:rxandroid:3.0.0'
  rxBinding = 'com.jakewharton.rxbinding3:rxbinding:3.1.0'
  junit = 'junit:junit:4.12'
  truth = 'com.google.truth:truth:0.36'

  // Lint dependencies.
  lintApi = "com.android.tools.lint:lint-api:${versions.lint}"
  lint = "com.android.tools.lint:lint:${versions.lint}"
  lintTests = "com.android.tools.lint:lint-tests:${versions.lint}"
}

configurations {
  osstrich
}
dependencies {
  osstrich 'com.squareup.osstrich:osstrich:1.2.0'
}
task publishV1Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite'
  ]
}
task publishV2Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite2'
  ]
}
task publishV3Javadoc(type: JavaExec) {
  classpath = configurations.osstrich
  main = 'com.squareup.osstrich.JavadocPublisher'
  args = [
      'build/javadoc',
      'https://github.com/square/sqlbrite',
      'com.squareup.sqlbrite3'
  ]
}
task publishJavadoc(dependsOn: [publishV1Javadoc, publishV2Javadoc, publishV3Javadoc])
